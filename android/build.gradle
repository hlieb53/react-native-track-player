import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

apply plugin: 'com.android.library'

def exoplayer = false, cache = false, dash = false, hls = false, smoothstreaming = false
def castAppId = 'null', auto = false

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    exoplayer = json.exoplayer || json.dash || json.hls || json.smoothstreaming
    cache = json.cache
    dash = json.dash
    hls = json.hls
    smoothstreaming = json.smoothstreaming
    castAppId = json.castApplicationId
    auto = json.auto
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'CAST_APPLICATION_ID', castAppId
            def autoMd = auto ? 'com.google.android.gms.car.application' : 'disabled.car.application'
            it.manifestPlaceholders = [autoMetadata: autoMd]
        }
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    compile 'com.facebook.react:react-native:+'

    // ExoPlayer Core
    if(exoplayer) {
        compile 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    }

    // ExoPlayer DASH
    if(dash) {
        compile 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    }

    // ExoPlayer HLS
    if(hls) {
        compile 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    }

    // ExoPlayer SmoothStreaming
    if(smoothstreaming) {
        compile 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    }

    // Caching (for the default Android player)
    if(cache && !exoplayer) {
        compile 'com.danikula:videocache:2.7.0'
    } else {
        provided 'com.danikula:videocache:2.7.0'
    }

    // Google Cast
    compile 'com.google.android.gms:play-services-cast-framework:11.0.2'
}
