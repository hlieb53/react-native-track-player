buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def minSdkFallback = 21
def targetSdkFallback = 31

android {
    compileSdkVersion safeExtGet("compileSdkVersion", targetSdkFallback)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", minSdkFallback) // RN's minimum version
        targetSdkVersion safeExtGet("targetSdkVersion", targetSdkFallback)

        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        // Make sure you have installed the react-native npm package before compiling
        url "../node_modules/react-native/android"
    }

    mavenCentral()
    google()
}

dependencies {
    implementation "com.github.DoubleSymmetry:KotlinAudio:v0.1.28"

    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'

    // Make sure we're using androidx
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01"
    implementation "com.orhanobut:logger:2.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
}
