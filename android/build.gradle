import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    compile 'com.facebook.react:react-native:+'


    def exoplayer = false, cache = false, chromecast = false;
    File file = file('../../../track-player.json');
    if(file.exists()) {
        def json = new JsonSlurper().parseText(file.text);
        exoplayer = json.dash || json.hls || json.smoothstreaming;
        cache = json.cache;
        chromecast = json.chromecast;
    }

    if(exoplayer) {
        compile 'com.google.android.exoplayer:exoplayer:r2.3.1'
    } else {
        provided 'com.google.android.exoplayer:exoplayer:r2.3.1'
    }

    if(cache && !exoplayer) {
        compile 'com.danikula:videocache:2.6.4'
    } else {
        provided 'com.danikula:videocache:2.6.4'
    }

    if(chromecast) {
        compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.9.1'
    } else {
        provided 'com.google.android.libraries.cast.companionlibrary:ccl:2.9.1'
    }

}
