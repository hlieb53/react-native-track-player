import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

apply plugin: 'com.android.library'

String castAppId = null;
boolean exoplayer = false, cache = false, auto = false;
boolean dash = false, hls = false, smoothstreaming = false;

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    cache = json.cache ?: cache
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
    castAppId = json.castApplicationId ?: castAppId
    auto = json.auto ?: auto

    exoplayer = json.exoplayer || dash || hls || smoothstreaming
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    buildTypes {
        buildTypes.each {
            def appId = castAppId != null ? '"' + castAppId + '"' : 'null';
            it.buildConfigField 'String', 'CAST_APPLICATION_ID', appId

            def autoMd = auto ? 'com.google.android.gms.car.application' : 'disabled.car.application'
            it.manifestPlaceholders = [autoMetadata: autoMd]
        }
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    compile 'com.facebook.react:react-native:+'

    // ExoPlayer Core
    if(exoplayer) {
        compile 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    }

    // ExoPlayer DASH
    if(dash) {
        compile 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    }

    // ExoPlayer HLS
    if(hls) {
        compile 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    }

    // ExoPlayer SmoothStreaming
    if(smoothstreaming) {
        compile 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    }

    // Caching (for the default Android player)
    if(cache && !exoplayer) {
        compile 'com.danikula:videocache:2.7.0'
    } else {
        provided 'com.danikula:videocache:2.7.0'
    }

    // Google Cast (Update this when RN updates the support library to 25+)
    compile 'com.google.android.gms:play-services-cast-framework:9.4.0'

    // Make sure we're using at least the support library 23.2.0, not 23.0.1
    compile 'com.android.support:appcompat-v7:23.2.0'
}
